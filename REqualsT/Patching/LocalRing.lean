import Mathlib
import REqualsT.Patching.VanishingFilter

set_option autoImplicit false
variable (Râ‚€ : Type*) [CommRing Râ‚€]
variable {Î¹ : Type*} {R M : Î¹ â†’ Type*} [âˆ€ i, CommRing (R i)] [âˆ€ i, AddCommGroup (M i)]
variable [âˆ€ i, Algebra Râ‚€ (R i)] [âˆ€ i, Module (R i) (M i)]
variable (I : âˆ€ i, Ideal (R i)) (N : âˆ€ i, Submodule (R i) (M i))
variable [âˆ€ i, IsLocalRing (R i)]


open IsLocalRing

local notation "ð”­(" F ")" => eventuallyProd (maximalIdeal <| R Â·) F

lemma le_eventuallyProd_vanishingFilter (p : Ideal (Î  i, R i)) :
    p â‰¤ ð”­(vanishingFilter p) :=
  fun _ â†¦ eventually_vanishingFilter_not_isUnit _

lemma eventuallyProd_vanishingFilter_eq_iff {p : Ideal (Î  i, R i)} :
    ð”­(vanishingFilter p) = p â†”
      Ideal.jacobson âŠ¥ â‰¤ p := by
  rw [le_antisymm_iff, and_iff_left (le_eventuallyProd_vanishingFilter _),
    eventuallyProd_eq_sup, sup_le_iff, and_iff_right (eventuallyProd_vanishingFilter_le _),
    â† pi'_jacobson]
  congr! with i
  exact (IsLocalRing.jacobson_eq_maximalIdeal _ (by simp)).symm

lemma eventuallyProd_vanishingFilter (p : Ideal (Î  i, R i))
    (hp : Ideal.jacobson âŠ¥ â‰¤ p) :
    ð”­(vanishingFilter p) = p :=
  eventuallyProd_vanishingFilter_eq_iff.mpr hp

@[simps]
def IsLocalRing.idealPiEquiv :
    { I : Ideal (Î  i, R i) // Ideal.jacobson âŠ¥ â‰¤ I } â‰ƒ Filter Î¹ where
  toFun p := vanishingFilter p.1
  invFun F := âŸ¨ð”­(F), by
    rw [â† eventuallyProd_vanishingFilter_eq_iff, vanishingFilter_eventuallyProd]
    exact fun i â†¦ Ideal.IsPrime.ne_top'âŸ©
  left_inv p := Subtype.ext (eventuallyProd_vanishingFilter_eq_iff.mpr p.2)
  right_inv F := vanishingFilter_eventuallyProd _ _ (fun i â†¦ Ideal.IsPrime.ne_top')

def IsLocalRing.idealPiQuotientEquiv :
    Ideal ((Î  i, R i) â§¸ (Ideal.jacobson (R := Î  i, R i) âŠ¥)) â‰ƒ Filter Î¹ :=
  (Ideal.idealQuotientEquiv _).trans idealPiEquiv

@[simps]
def IsLocalRing.idealPiIsPrimeEquiv :
    { I : Ideal (Î  i, R i) // I.IsPrime âˆ§ Ideal.jacobson âŠ¥ â‰¤ I } â‰ƒ Ultrafilter Î¹ where
  toFun p := letI := p.2.1; vanishingUltrafilter p.1
  invFun F := âŸ¨ð”­(F), by
    rw [â† eventuallyProd_vanishingFilter_eq_iff, vanishingFilter_eventuallyProd]
    exacts [âŸ¨inferInstance, rflâŸ©, fun i â†¦ Ideal.IsPrime.ne_top']âŸ©
  left_inv p := Subtype.ext (eventuallyProd_vanishingFilter_eq_iff.mpr p.2.2)
  right_inv F := Ultrafilter.coe_injective
    (vanishingFilter_eventuallyProd _ _ (fun i â†¦ Ideal.IsPrime.ne_top'))

variable (F : Ultrafilter Î¹)

section UltraProduct

variable (R)

abbrev UltraLocalization := Localization.AtPrime ð”­(F)

lemma ker_algebraMap_localization_eventuallyProd :
    RingHom.ker (algebraMap (Î  i, R i) (UltraLocalization R F)) =
      eventuallyProd âŠ¥ F := by
  ext x
  suffices (âˆƒ a, (âˆ€á¶  i in F, IsUnit (a i)) âˆ§ a * x = 0) â†” âˆ€á¶  i in F, x i = 0 by
    simp only [RingHom.mem_ker, mem_eventuallyProd,
      IsLocalization.map_eq_zero_iff (M := Ideal.primeCompl ð”­(F))]
    simpa [Ideal.primeCompl]
  constructor
  Â· rintro âŸ¨a, hâ‚, hâ‚‚âŸ©
    filter_upwards [hâ‚] with i hi
    apply hi.mul_right_injective
    simpa using congr_fun hâ‚‚ i
  Â· intro H
    classical
    refine âŸ¨(if x Â· = 0 then 1 else 0), ?_, by aesopâŸ©
    filter_upwards [H] with i hi
    simp [hi]

def UltraProduct.toLocalization :
    UltraProduct R F â†’â‚[Râ‚€] UltraLocalization R F :=
  Ideal.Quotient.liftâ‚ (eventuallyProd (R := R) (M := R) âŠ¥ F)
    (IsScalarTower.toAlgHom _ _ _)
    (ker_algebraMap_localization_eventuallyProd R F).ge

lemma UltraProduct.toLocalization_injective :
    Function.Injective (toLocalization Râ‚€ R F) :=
  RingHom.lift_injective_of_ker_le_ideal _ _
    (ker_algebraMap_localization_eventuallyProd R F).le

variable (M) in
abbrev UltraModuleLocalization := LocalizedModule (Ideal.primeCompl ð”­(F)) (Î  i, M i)

variable (M) in
abbrev UltraModuleLocalization.mkLinearMap : (Î  i, M i) â†’â‚—[Î  i, R i] UltraModuleLocalization R M F :=
  LocalizedModule.mkLinearMap _ _

-- set_option diagnostics true in
lemma ker_mkLinearMap_localization_eventuallyProd :
    LinearMap.ker (LocalizedModule.mkLinearMap (Ideal.primeCompl ð”­(F)) (Î  i, M i)) =
      eventuallyProd âŠ¥ F := by
  ext x
  suffices (âˆƒ a : Î  i, R i, (âˆ€á¶  i in F, IsUnit (a i)) âˆ§ a â€¢ x = 0) â†” âˆ€á¶  i in F, x i = 0 by
    simpa only [Ideal.primeCompl, LinearMap.mem_ker, LocalizedModule.mkLinearMap_apply,
      â† LocalizedModule.zero_mk (s := 1), LocalizedModule.mk_eq, one_smul, Submonoid.smul_def,
      smul_zero (M := Î  i, R i), Subtype.exists, Submonoid.mem_mk, Subsemigroup.mem_mk,
      SetLike.mem_coe, mem_eventuallyProd, mem_maximalIdeal, mem_nonunits_iff, Set.mem_compl_iff,
      Filter.not_eventually, not_not, Ultrafilter.frequently_iff_eventually, exists_prop,
      Pi.bot_apply, Submodule.mem_bot]
  constructor
  Â· rintro âŸ¨a, hâ‚, hâ‚‚âŸ©
    filter_upwards [hâ‚] with i hi
    rw [â† hi.smul_left_cancel]
    simpa using congr_fun hâ‚‚ i
  Â· intro H
    classical
    refine âŸ¨(if x Â· = 0 then 1 else 0), ?_, by ext; simpâŸ©
    filter_upwards [H] with i hi
    simp [hi]

variable (M) in
def UltraProduct.toModuleLocalization :
    UltraProduct M F â†’â‚—[Î  i, R i] UltraModuleLocalization R M F :=
  Submodule.liftQ (eventuallyProd (R := R) (M := M) âŠ¥ F)
    (LocalizedModule.mkLinearMap (Ideal.primeCompl ð”­(F)) (Î  i, M i))
    (ker_mkLinearMap_localization_eventuallyProd ..).ge

@[simp]
lemma UltraProduct.toModuleLocalization_Ï€â‚— (x) :
    toModuleLocalization R M F (Ï€â‚— R M F x) =
      UltraModuleLocalization.mkLinearMap R M F x := rfl

lemma UltraProduct.toModuleLocalization_injective :
    Function.Injective (toModuleLocalization R M F) := by
  rw [â† LinearMap.ker_eq_bot, toModuleLocalization, Submodule.ker_liftQ_eq_bot]
  exact (ker_mkLinearMap_localization_eventuallyProd ..).le

end UltraProduct

section Finite

variable [Finite Râ‚€] (H : âˆ€á¶  i in F, Function.Bijective (algebraMap Râ‚€ (R i)))

include H in
/--
If `Râ‚€` is a finite cardinality local ring, and `Ráµ¢` is a family of `Râ‚€` algebras
such that `F`-many of them are isomorphic to `Râ‚€`,
then the localization `Î  Ráµ¢` at `ð”­(F)` is isomorphic to `Râ‚€`.
-/
lemma UltraLocalization.bijective_of_eventually_bijective :
    Function.Bijective (algebraMap Râ‚€ (UltraLocalization R F)) := by
  classical
  constructor
  Â· rw [injective_iff_map_eq_zero]
    intros x hx
    rw [IsScalarTower.algebraMap_apply Râ‚€ (Î  i, R i) (Localization.AtPrime _),
      IsLocalization.map_eq_zero_iff (M := Ideal.primeCompl ð”­(F))] at hx
    obtain âŸ¨a, haâ‚, haâ‚‚âŸ© : âˆƒ a : Î  i, R i, (âˆ€á¶  i in F, IsUnit (a i)) âˆ§
        âˆ€ i, a i * (algebraMap Râ‚€ _) x = 0 := by
      simpa [Ideal.primeCompl, funext_iff] using hx
    obtain âŸ¨i, hi, hi'âŸ© := (H.and haâ‚).exists
    apply hi.1
    simpa only [mul_zero, â† mul_assoc, IsUnit.val_inv_mul, one_mul, map_zero]
      using congr(hi'.unitâ»Â¹ * $(haâ‚‚ i))
  Â· intro x
    obtain âŸ¨x, âŸ¨s, hsâŸ©, rflâŸ© := IsLocalization.mk'_surjective (Ideal.primeCompl ð”­(F)) x
    simp_rw [IsScalarTower.algebraMap_apply Râ‚€ (Î  i, R i) (Localization.AtPrime _),
      â† IsLocalization.mk'_one (M := Ideal.primeCompl ð”­(F)),
      IsLocalization.mk'_eq_iff_eq (M := Ideal.primeCompl ð”­(F)),
      IsLocalization.eq_iff_exists (Ideal.primeCompl ð”­(F))]
    simp only [Ideal.primeCompl, OneMemClass.coe_one, one_mul, funext_iff, Pi.mul_apply,
      Pi.algebraMap_apply, Subtype.exists, Submonoid.mem_mk, Subsemigroup.mem_mk, Set.mem_compl_iff,
      SetLike.mem_coe, mem_eventuallyProd, mem_maximalIdeal, mem_nonunits_iff, Filter.not_eventually,
      not_not, Ultrafilter.frequently_iff_eventually, exists_prop] at hs âŠ¢
    have : âˆ€á¶  i in F, âˆƒ b : Râ‚€, s i * algebraMap Râ‚€ (R i) b = x i := by
      filter_upwards [H, hs] with i hi hsi
      obtain âŸ¨z, hzâŸ© := hi.2 (hsi.unitâ»Â¹ * x i)
      exact âŸ¨z, by rw [hz, â† mul_assoc, IsUnit.mul_val_inv, one_mul]âŸ©
    obtain âŸ¨b, hbâŸ© := F.eventually_exists_of_finite.mp this
    refine âŸ¨b, (fun i â†¦ if s i * algebraMap _ _ b = x i then 1 else 0), ?_, ?_âŸ©
    Â· filter_upwards [hb] with i hi
      rw [if_pos hi]; exact isUnit_one
    Â· intro i
      simp only [ite_mul, one_mul, zero_mul]
      split_ifs
      Â· assumption
      Â· rfl

include H in
lemma UltraProduct.bijective_toLocalization_of_eventually_bijective :
    Function.Bijective (toLocalization Râ‚€ R F) :=
  âŸ¨toLocalization_injective .., .of_comp (g := algebraMap Râ‚€ _) <| by
    convert (UltraLocalization.bijective_of_eventually_bijective Râ‚€ F H).2âŸ©

include H in
lemma UltraProduct.bijective_algebraMap_of_eventually_bijective :
    Function.Bijective (algebraMap Râ‚€ (UltraProduct R F)) :=
  (Function.Bijective.of_comp_iff' (bijective_toLocalization_of_eventually_bijective Râ‚€ F H) _).mp
    (by convert UltraLocalization.bijective_of_eventually_bijective Râ‚€ F H)

variable [âˆ€ i, Module Râ‚€ (M i)] [âˆ€ i, IsScalarTower Râ‚€ (R i) (M i)]

include H in
lemma UltraProduct.bijective_toModuleLocalization_of_eventually_bijective :
    Function.Bijective (UltraProduct.toModuleLocalization R M F) := by
  classical
  refine âŸ¨UltraProduct.toModuleLocalization_injective .., ?_âŸ©
  intro z
  induction' z using LocalizedModule.induction_on with m s
  obtain âŸ¨t, htâŸ© := (UltraLocalization.bijective_of_eventually_bijective Râ‚€ F H).2 (Localization.mk 1 s)
  refine âŸ¨t â€¢ .Ï€â‚— R M F m, ?_âŸ©
  rw [â† IsScalarTower.algebraMap_smul (Î  i, R i), map_smul,
    â† IsScalarTower.algebraMap_smul (UltraLocalization R F),
    â† IsScalarTower.algebraMap_apply, ht]
  show Localization.mk 1 s â€¢ LocalizedModule.mk m 1 = _
  rw [LocalizedModule.mk_smul_mk, one_smul, mul_one]

lemma UltraProduct.bijective_toModuleLocalization [IsLocalRing Râ‚€] :
    Function.Bijective (UltraProduct.toModuleLocalization (fun _ â†¦ Râ‚€) M F) :=
  bijective_toModuleLocalization_of_eventually_bijective Râ‚€ _ (.of_forall fun _ â†¦ Function.bijective_id)

end Finite
